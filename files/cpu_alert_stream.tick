// cpu_alert_stream

// metric: usage_user
// available_fields: "usage_guest","usage_guest_nice","usage_idle","usage_iowait", "usage_irq","usage_nice","usage_softirq","usage_steal","usage_system"

// TELEGRAF CONFIGURATION
// [[inputs.cpu]]
//   percpu = true
//   totalcpu = true
//   fielddrop = ["time_*"]

// DEFINE: kapacitor define cpu_alert_stream -type stream -tick cpu/cpu_alert_stream.tick -dbrp telegraf.autogen
// ENABLE: kapacitor enable cpu_alert_stream

// Parameters
var info = 70
var warn = 80
var crit = 90
var infoSig = 3.5
var warnSig = 4
var critSig = 4.5
var period = 5m
var every = 5m

// Dataframe
var data = stream
    |from()
        .database('telegraf')
        .retentionPolicy('autogen')
        .measurement('cpu')
        .groupBy('host', 'service', 'datacenter')
        .where(lambda: "cpu" == 'cpu-total')
    |window()
        .period(period)
        .every(every)
    |mean('usage_user')
        .as('stat')

// Thresholds
var alert = data
    |eval(lambda: sigma("stat"))
        .as('sigma')
        .keep()
    |alert()
        .id('{{ .Name }}/{{ index .Tags "service" }}/{{ index .Tags "datacenter" }}/{{ index .Tags "host" }}')
        .message('{{ .ID }} is {{ .Level }} mean:{{ index .Fields "stat" }} stddev:{{ index .Fields "sigma" }} @channel')
        //.info(lambda: "stat" > info OR "sigma" > infoSig)
        .warn(lambda: "stat" > warn OR "sigma" > warnSig)
        .crit(lambda: "stat" > crit OR "sigma" > critSig)
    
// Alert
alert
    .email()
    .slack()
    .log('/tmp/cpu_alert_stream.log')
